apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.ang.acb.todolearn"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }


    testOptions.unitTests {
        // Fix Robolectric Warning: "No such manifest file: ./AndroidManifest.xml"
        // https://developer.android.com/training/testing/fundamentals#small-tests
        includeAndroidResources = true
        // Fix Error "Method isEmpty in android.text.TextUtils not mocked"
        // https://developer.android.com/training/testing/unit-testing/local-unit-tests.html#error-not-mocked
        returnDefaultValues = true
    }

    // Sharing code between unit tests and instrumentation tests
    // https://blog.danlew.net/2015/11/02/sharing-code-between-unit-tests-and-instrumentation-tests-on-android/
    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android KTX
    implementation "androidx.core:core-ktx:$androidx_core_version"

    // Support libraries
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.legacy:legacy-support-v4:$androidx_legacy_support_version"
    implementation "androidx.annotation:annotation:$androidx_annotation_version"
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.test.espresso:espresso-idling-resource:$espresso_idling_version"

    // ViewModel & LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$arch_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$arch_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$arch_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$arch_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$arch_lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Paging
    implementation "androidx.paging:paging-runtime:$paging_version"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigation_version"

    // WorkManager
    implementation "android.arch.work:work-runtime-ktx:$work_version"

    // Coroutines for getting work off the UI thread
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Timber for logging
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Testing code should not be included in the main code.
    // Once https://issuetracker.google.com/128612536 is fixed this can be fixed.
    implementation "androidx.test:core:$androidx_test_core_version"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"

    // Dependencies for local tests
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrest_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "androidx.test:core:$androidx_test_core_version"
    testImplementation "androidx.test.ext:junit:$androidx_test_ext_junit"
    testImplementation "androidx.arch.core:core-testing:$arch_testing_version"
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"

    // Dependencies for instrumented tests
    androidTestImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.arch.core:core-testing:$arch_testing_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$mockito_dexmaker_version"
    androidTestImplementation "androidx.test:core-ktx:$androidx_test_core_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_ext_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
}
